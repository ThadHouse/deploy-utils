plugins {
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "idea"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
    jcenter()
}

dependencies {
    api 'com.jcraft:jsch:0.1.55'
    api 'com.google.code.gson:gson:2.8.6'

    testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation('cglib:cglib-nodep:2.2')
    testImplementation('org.objenesis:objenesis:1.2')
    testImplementation gradleTestKit()
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group = "edu.wpi.first"
archivesBaseName = "DeployUtils"
version = "2021.02.06" // YYYY.MM.DD(revision)

sourceCompatibility = 11
targetCompatibility = 11

pluginBundle {
    website = 'https://github.com/wpilibsuite/deploy-utils'
    vcsUrl = 'https://github.com/wpilibsuite/deploy-utils'
    description = 'Additions to the model-based DSL for deploying Java and Native projects to remote targets'
    tags = ['remote', 'target', 'deploy', 'java', 'native']
}

gradlePlugin {
    plugins {
        DeployUtils {
            id = 'edu.wpi.first.DeployUtils'
            displayName = 'DeployUtils'
            implementationClass = 'edu.wpi.first.deployutils.DeployUtils'
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

wrapper {
    gradleVersion = '6.8.1'
    distributionType = Wrapper.DistributionType.BIN
}

plugins {
    id "com.gradle.plugin-publish" version "1.2.1"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "idea"
}

repositories {
    maven {
        url = "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'

    api 'org.apache.sshd:sshd-core:2.13.2'
    api 'org.apache.sshd:sshd-sftp:2.13.2'
    implementation 'net.i2p.crypto:eddsa:0.3.0'

    testImplementation('org.spockframework:spock-core:2.4-M6-groovy-4.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group = "org.wpilib"
version = "2027.0.0"

base {
    archivesName = "DeployUtils"
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/deploy-utils'
    vcsUrl = 'https://github.com/wpilibsuite/deploy-utils'
    plugins {
        DeployUtils {
            id = 'org.wpilib.DeployUtils'
            displayName = 'DeployUtils'
            implementationClass = 'org.wpilib.deployutils.DeployUtils'
            description = 'Additions to the model-based DSL for deploying Java and Native projects to remote targets'
            tags = ['remote', 'target', 'deploy', 'java', 'native']
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

def examplesFolder = file("$rootDir/testing")

tasks.register('PatchExamples') {
    doLast {
        String regex = "(id\\s*?[\\\"|\\']org\\.wpilib\\.DeployUtils[\\\"|\\'].*?version\\s*?[\\\"|\\'])(.+?)([\\\"|\\'])";

        examplesFolder.eachFile { File file ->
            if (file.isDirectory() && file.name != '_archived') {
                def buildGradleFile = new File(file, 'build.gradle')
                if (buildGradleFile.exists() && buildGradleFile.isFile()) {
                    def text = buildGradleFile.text
                    text = text.replaceAll(regex, "id \"org.wpilib.DeployUtils\" version \"${version}\"")
                    buildGradleFile.text = text
                }
            }
        }
    }
}

build.dependsOn PatchExamples
jar.finalizedBy PatchExamples

wrapper {
    gradleVersion = '9.2.0'
    distributionType = Wrapper.DistributionType.BIN
}
